plugins {
    id 'com.android.library'
    id 'kotlin-android'

    id 'maven-publish'
    id 'signing'
}

group = property("group")
version = property("version")

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                artifactId = 'expansionpanel'

                from components.release

                pom {
                    name = 'ExpansionLayout Library'
                    description = 'Lightweigt and customizable android library for expandable layout.'
                    url = 'https://github.com/C0rr4do/ExpansionLayout'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'timd'
                            name = 'Tim Depner'
                            email = 'tim.depner@web.de'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/C0rr4do/ExpansionLayout.git'
                        developerConnection = 'scm:git:ssh://github.com/C0rr4do/ExpansionLayout.git'
                        url = 'https://github.com/C0rr4do/ExpansionLayout'
                    }
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username = "$NEXUS_USERNAME"
                    password = "$NEXUS_PASSWORD"
                }

                def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
    signing {
        sign publishing.publications.release
    }
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 16
        targetSdk 31
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
}
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}